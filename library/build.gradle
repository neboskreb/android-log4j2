plugins {
    id 'com.android.library'
    id 'de.mannodermaus.android-junit5'
    id 'maven-publish'
    id 'org.jreleaser'
}

def libraryGroup    = 'io.github.neboskreb'
def libraryArtifact = 'android-log4j2'
def libraryVersion  = '2.24.1-SNAPSHOT'

final stagingDirectory = layout.buildDirectory.dir('staging-deploy')

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = 'staging'
                url = stagingDirectory
            }
        }

        publications {
            release(MavenPublication) {
                group = libraryGroup
                artifactId = libraryArtifact
                version = libraryVersion

                afterEvaluate {
                    from components.release
                }

                pom {
                    name = 'Android-Log4J'
                    description = 'Support library for using Log4J over SLF4J on Android'
                    url = 'https://github.com/neboskreb/android-log4j2'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'JBM'
                            name = 'John Y. Pazekha'
                            email = 'neboskreb@narod.ru'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:neboskreb/android-log4j2.git'
                        developerConnection = 'scm:git:ssh://github.com:neboskreb/android-log4j2.git'
                        url = 'https://github.com/neboskreb/android-log4j2'
                    }
                }
            }
        }
    }

    jreleaser {
        gitRootSearch = true
        signing {
            active = 'RELEASE'
            armored = true
            verify = false

            version = libraryVersion

            secretKey  = getConfiguredValue('signing.key.armored', 'PGP_SIGNING_KEY_ARMORED')
            passphrase = getConfiguredValue('signing.password',    'PGP_SIGNING_PASSWORD')
        }
        deploy {
            maven {
                final String staging = stagingDirectory.get().toString()

                github {
                    packages {
                        active = 'RELEASE'
                        url = 'https://maven.pkg.github.com/neboskreb/android-log4j2'
                        stagingRepository(staging)

                        sign = true

                        username = getConfiguredValue('github_packages_user', 'PACKAGES_USERNAME')
                        password = getConfiguredValue('github_packages_token', 'PACKAGES_TOKEN')
                    }
                }

                mavenCentral {
                    sonatype {
                        active = 'RELEASE'
                        url = 'https://central.sonatype.com/api/v1/publisher'
                        stagingRepository(staging)

                        sign = true

                        username = getConfiguredValue('mavenCentralUser', 'MAVEN_USERNAME')
                        password = getConfiguredValue('mavenCentralPass', 'MAVEN_PASSWORD')

                        // Android release contains .aar instead of .jar, which is not currently supported by the rules validator.
                        // See https://github.com/jreleaser/jreleaser.github.io/issues/85
                        // TODO re-enable the rules check once the .aar is supported
                        applyMavenCentralRules = false
                    }
                }
            }
        }
    }

    tasks.named('jreleaserDeploy') {
        dependsOn publishReleasePublicationToStagingRepository
    }
}

android {
    namespace = 'com.github.neboskreb.log4j2'
    compileSdk = 35

    defaultConfig {
        minSdk = 26

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
    }

    publishing {
        singleVariant("release") {
            withJavadocJar()
            withSourcesJar()
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    // ==== Log4j
    //noinspection GradleDependency
    api 'org.apache.logging.log4j:log4j-slf4j2-impl:[2.24, 3.0)'
    //noinspection GradleDependency, AnnotationProcessorOnCompilePath
    api 'org.apache.logging.log4j:log4j-core:[2.24, 3.0)'
//    annotationProcessor 'org.apache.logging.log4j:log4j-core:[2.24, 3.0)'
    // ==== /Log4j

    // ==== Lombok
    //noinspection AnnotationProcessorOnCompilePath -- because Lombok annotations are contained in this package, so you get import errors without it
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    // ==== /Lombok


    // To avoid elevating the JAVA VERSION to 17, which is required by JUnit 6+, we shall keep using JUnit 5:
    testImplementation platform('org.junit:junit-bom:[5.14.0, 6.0)')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly    'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.21.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.21.0'
    testImplementation 'org.assertj:assertj-core:3.27.6'


    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    // To avoid elevating the JAVA VERSION to 17, which is required by JUnit 6+, we shall keep using JUnit 5:
    androidTestImplementation platform('org.junit:junit-bom:[5.14.0, 6.0)')
    androidTestImplementation 'org.junit.jupiter:junit-jupiter-api'
    androidTestImplementation 'org.junit.jupiter:junit-jupiter'
    androidTestImplementation 'org.junit.platform:junit-platform-launcher'
    androidTestImplementation 'org.mockito:mockito-android:5.21.0'
    androidTestImplementation 'org.mockito:mockito-junit-jupiter:5.21.0'
    androidTestImplementation 'org.assertj:assertj-core:3.27.6'
}


private String getConfiguredValue(String projectProperty, String environmentVariable) {
    return project.findProperty(projectProperty) ?: System.getenv(environmentVariable)
}
