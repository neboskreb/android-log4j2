plugins {
    id 'com.android.library'
    id 'de.mannodermaus.android-junit5'
    id 'maven-publish'
    id 'signing'
}

def libraryGroup    = 'io.github.neboskreb'
def libraryArtifact = 'android-log4j2'
def libraryVersion  = '2.24.1-SNAPSHOT'

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "github-packages"
                url = uri("https://maven.pkg.github.com/neboskreb/android-log4j2")
                credentials {
                    username = project.findProperty("github_packages_user") ?: System.getenv("PACKAGES_USERNAME")
                    password = project.findProperty("github_packages_token") ?: System.getenv("PACKAGES_TOKEN")
                }
            }
            maven {
                name "MavenCentral"
                if (version.endsWith('SNAPSHOT')) {
                    url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
                } else  {
                    url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                }
                credentials {
                    username = project.findProperty("mavenCentralUser") ?: System.getenv("MAVEN_USERNAME")
                    password = project.findProperty("mavenCentralPass") ?: System.getenv("MAVEN_PASSWORD")
                }
            }
        }

        publications {
            release(MavenPublication) {
                group = libraryGroup
                artifactId = libraryArtifact
                version = libraryVersion

                afterEvaluate {
                    from components.release
                }

                pom {
                    name = 'Android-Log4J'
                    description = 'Support library for using Log4J over SLF4J on Android'
                    url = 'https://github.com/neboskreb/android-log4j2'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'JBM'
                            name = 'John Y. Pazekha'
                            email = 'neboskreb@narod.ru'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:neboskreb/android-log4j2.git'
                        developerConnection = 'scm:git:ssh://github.com:neboskreb/android-log4j2.git'
                        url = 'https://github.com/neboskreb/android-log4j2'
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.release
    }
}

android {
    namespace = 'com.github.neboskreb.log4j2'
    compileSdk = 35

    defaultConfig {
        minSdk = 26

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    publishing {
        singleVariant("release") {
            withJavadocJar()
            withSourcesJar()
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    // ==== Log4j
    //noinspection GradleDependency
    api 'org.apache.logging.log4j:log4j-slf4j2-impl:[2.24, 3.0)'
    //noinspection GradleDependency, AnnotationProcessorOnCompilePath
    api 'org.apache.logging.log4j:log4j-core:[2.24, 3.0)'
//    annotationProcessor 'org.apache.logging.log4j:log4j-core:[2.24, 3.0)'
    // ==== /Log4j

    // ==== Lombok
    //noinspection AnnotationProcessorOnCompilePath -- because Lombok annotations are contained in this package, so you get import errors without it
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    // ==== /Lombok


    testImplementation platform('org.junit:junit-bom:5.14.0')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly    'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.20.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.20.0'
    testImplementation 'org.assertj:assertj-core:3.27.6'


    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation platform('org.junit:junit-bom:5.14.0')
    androidTestImplementation 'org.junit.jupiter:junit-jupiter-api'
    androidTestImplementation 'org.junit.jupiter:junit-jupiter'
    androidTestImplementation 'org.junit.platform:junit-platform-launcher'
    androidTestImplementation 'org.mockito:mockito-android:5.20.0'
    androidTestImplementation 'org.mockito:mockito-junit-jupiter:5.20.0'
    androidTestImplementation 'org.assertj:assertj-core:3.27.6'
}
